plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.0.0'
    id 'net.kyori.blossom' version '1.2.0'
}

group = "$projectGroup"
version = "$projectVersion"

repositories {
    mavenCentral()
    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        url 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        url 'https://reposilite.koboo.eu/releases'
    }
    maven {
        url 'https://hub.jeff-media.com/nexus/repository/jeff-media-public/'
    }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.20-R0.1-SNAPSHOT'

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    compileOnly "org.spigotmc:plugin-annotations:$pluginAnnotationsVersion"
    annotationProcessor "org.spigotmc:plugin-annotations:$pluginAnnotationsVersion"

    implementation 'eu.koboo:yaml-config:1.3.3'

    // https://github.com/JEFF-Media-GbR/MorePersistentDataTypes
    implementation 'com.jeff_media:MorePersistentDataTypes:2.4.0'

    // https://github.com/JEFF-Media-GbR/Spigot-UpdateChecker
    implementation 'com.jeff_media:SpigotUpdateChecker:3.0.3'

    //https://github.com/Bastian/bstats-metrics
    implementation 'org.bstats:bstats-bukkit:3.0.2'
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
    targetCompatibility = JavaVersion.VERSION_17
    sourceCompatibility = JavaVersion.VERSION_17
}

compileJava {
    options.fork = true
    options.incremental = true
    options.encoding = 'UTF-8'
    options.release.set(17)
}

blossom {
    replaceToken('PROJECT_NAME', project.name)
    replaceToken('PROJECT_VERSION', project.version)
    replaceToken('PROJECT_GROUP', projectGroup)
    replaceToken('PROJECT_DESCRIPTION', projectDescription)
    replaceToken('PROJECT_WEBSITE', projectWebsite)
}

shadowJar {
    relocate 'com.jeff_media.morepersistentdatatypes', 'eu.koboo.backpacks.libs.morepersistentdatatypes'
    relocate 'com.jeff_media.updatechecker', 'eu.koboo.backpacks.libs.updatechecker'
    relocate 'org.bstats', 'eu.koboo.backpacks.libs.bstats'
    relocate 'eu.koboo.yaml', 'eu.koboo.backpacks.libs.yaml'
}

publishing {
    repositories {
        maven {
            name 'koboo'
            url 'https://reposilite.koboo.eu/releases'
            credentials {
                username System.getenv('REPO_USER')
                password System.getenv('REPO_TOKEN')
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}
